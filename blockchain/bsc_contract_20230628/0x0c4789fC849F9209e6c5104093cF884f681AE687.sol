// File: @openzeppelin/contracts/security/ReentrancyGuard.sol


// OpenZeppelin Contracts (last updated v4.8.0) (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        // On the first call to nonReentrant, _status will be _NOT_ENTERED
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;
    }

    function _nonReentrantAfter() private {
        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// File: @uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router01.sol

pragma solidity >=0.6.2;

interface IUniswapV2Router01 {
    function factory() external pure returns (address);
    function WETH() external pure returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB, uint liquidity);
    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);
    function removeLiquidity(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETH(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountToken, uint amountETH);
    function removeLiquidityWithPermit(
        address tokenA,
        address tokenB,
        uint liquidity,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountA, uint amountB);
    function removeLiquidityETHWithPermit(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountToken, uint amountETH);
    function swapExactTokensForTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapTokensForExactTokens(
        uint amountOut,
        uint amountInMax,
        address[] calldata path,
        address to,
        uint deadline
    ) external returns (uint[] memory amounts);
    function swapExactETHForTokens(uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);
    function swapTokensForExactETH(uint amountOut, uint amountInMax, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapExactTokensForETH(uint amountIn, uint amountOutMin, address[] calldata path, address to, uint deadline)
        external
        returns (uint[] memory amounts);
    function swapETHForExactTokens(uint amountOut, address[] calldata path, address to, uint deadline)
        external
        payable
        returns (uint[] memory amounts);

    function quote(uint amountA, uint reserveA, uint reserveB) external pure returns (uint amountB);
    function getAmountOut(uint amountIn, uint reserveIn, uint reserveOut) external pure returns (uint amountOut);
    function getAmountIn(uint amountOut, uint reserveIn, uint reserveOut) external pure returns (uint amountIn);
    function getAmountsOut(uint amountIn, address[] calldata path) external view returns (uint[] memory amounts);
    function getAmountsIn(uint amountOut, address[] calldata path) external view returns (uint[] memory amounts);
}

// File: @uniswap/v2-periphery/contracts/interfaces/IUniswapV2Router02.sol

pragma solidity >=0.6.2;


interface IUniswapV2Router02 is IUniswapV2Router01 {
    function removeLiquidityETHSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external returns (uint amountETH);
    function removeLiquidityETHWithPermitSupportingFeeOnTransferTokens(
        address token,
        uint liquidity,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline,
        bool approveMax, uint8 v, bytes32 r, bytes32 s
    ) external returns (uint amountETH);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
    function swapExactETHForTokensSupportingFeeOnTransferTokens(
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external payable;
    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
}

// File: @uniswap/v2-core/contracts/interfaces/IUniswapV2Factory.sol

pragma solidity >=0.5.0;

interface IUniswapV2Factory {
    event PairCreated(address indexed token0, address indexed token1, address pair, uint);

    function feeTo() external view returns (address);
    function feeToSetter() external view returns (address);

    function getPair(address tokenA, address tokenB) external view returns (address pair);
    function allPairs(uint) external view returns (address pair);
    function allPairsLength() external view returns (uint);

    function createPair(address tokenA, address tokenB) external returns (address pair);

    function setFeeTo(address) external;
    function setFeeToSetter(address) external;
}

// File: @openzeppelin/contracts/token/ERC20/IERC20.sol


// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// File: @openzeppelin/contracts/utils/Context.sol


// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// File: @openzeppelin/contracts/access/Ownable.sol


// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;


/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// File: @openzeppelin/contracts/utils/Address.sol


// OpenZeppelin Contracts (last updated v4.8.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling
     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.
     *
     * _Available since v4.8._
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason or using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    function _revert(bytes memory returndata, string memory errorMessage) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert(errorMessage);
        }
    }
}

// File: @openzeppelin/contracts/utils/math/SafeMath.sol


// OpenZeppelin Contracts (last updated v4.6.0) (utils/math/SafeMath.sol)

pragma solidity ^0.8.0;

// CAUTION
// This version of SafeMath should only be used with Solidity 0.8 or later,
// because it relies on the compiler's built in overflow checks.

/**
 * @dev Wrappers over Solidity's arithmetic operations.
 *
 * NOTE: `SafeMath` is generally not needed starting with Solidity 0.8, since the compiler
 * now has built in overflow checking.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
            // benefit is lost if 'b' is also tested.
            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}

// File: Vestable.sol


pragma solidity 0.8.9;




abstract contract Vestable is Context, ReentrancyGuard, Ownable {
    using SafeMath for uint256;

    bool private vestingInit = false;

    struct Stage {
        uint256 date;
        uint256 tokensUnlockedPercentage;
    }

    struct User {
        uint256 initialBalance;
        uint256 totalWithdrawn;
        uint256 contribution;
        uint256 lastWithdrawTime;
    }

    struct Sale {
        address payable wallet;
        uint256 rate;
        uint256 divider;
        uint256 weiRaised;
        uint256 hardCap;
        uint256 capPerWallet;
        uint256 apy;
        uint256 tokenBalanceToDistribute;
        bool isActive;
        bool isClaimUnlocked;
    }

    mapping(uint8 => mapping(address => User)) public users;
    mapping(address => bool) public isWhitelisted;
    mapping(uint8 => Stage[]) public stages;
    uint256 minCap = 0.1 ether;
    Sale[3] public sales;

    uint8 public currentRound;

    event TokensPurchased(
        address indexed purchaser,
        uint256 value,
        uint256 amount,
        uint8 round
    );

    event Withdraw(uint256 tokensToSend, uint256 now);

    constructor() {}

    function initVestingSchedule(
        uint256 _rate,
        address payable _wallet,
        uint256 _divider,
        uint256 _hardCap,
        uint256 _capPerWallet
    ) external onlyOwner {
        require(!vestingInit);
        sales[0].isActive = true;
        sales[0].rate = _rate;
        sales[0].divider = _divider;
        sales[0].wallet = _wallet;
        sales[0].hardCap = _hardCap;
        sales[0].capPerWallet = _capPerWallet;
        vestingInit = true;
    }

    function vestingWhiteListUsers(address[] calldata _whitelist)
        external
        onlyOwner
    {
        for (uint256 i = 0; i < _whitelist.length; i++) {
            isWhitelisted[_whitelist[i]] = true;
        }
    }

    function removeFromVestingWhitelist(address[] calldata _removeUser)
        external
        onlyOwner
    {
        for (uint256 i = 0; i < _removeUser.length; i++) {
            isWhitelisted[_removeUser[i]] = false;
        }
    }

    function buyTokens() public payable nonReentrant {
        require(sales[currentRound].isActive, "Sale not active");
        require(
            msg.value >= minCap,
            "amount should be greator than min contribution"
        );
        uint256 weiAmount = msg.value;
        _preValidatePurchase(msg.sender, weiAmount, currentRound);

        // calculate token amount to be created
        uint256 tokens = _getTokenAmount(weiAmount, currentRound);

        // update state
        sales[currentRound].weiRaised = sales[currentRound].weiRaised.add(
            weiAmount
        );
        users[currentRound][msg.sender].contribution = users[currentRound][
            msg.sender
        ].contribution.add(weiAmount);

        _processPurchase(msg.sender, tokens, currentRound);
        emit TokensPurchased(msg.sender, weiAmount, tokens, currentRound);
        _forwardFunds(currentRound);
    }

    function _preValidatePurchase(
        address beneficiary,
        uint256 weiAmount,
        uint8 _round
    ) internal view {
        require(
            beneficiary != address(0),
            "Crowdsale: beneficiary is the zero address"
        );
        require(weiAmount != 0, "Crowdsale: weiAmount is 0");
        if (_round < 2) {
            require(isWhitelisted[beneficiary], "You're not whitelisted");
        }
        require(
            users[_round][beneficiary].contribution.add(weiAmount) <=
                sales[_round].capPerWallet,
            "Amount exceeds user limit"
        );
        require(
            sales[_round].weiRaised.add(weiAmount) <= sales[_round].hardCap,
            "Amount exceeds HardCap of the Round"
        );
    }

    function setMinCap(uint256 _mincap) external onlyOwner {
        require(
            _mincap > 0,
            " Min contribution amount should be greator than 0"
        );
        minCap = _mincap;
    }

    function _deliverTokens(
        address beneficiary,
        uint256 tokenAmount,
        uint8 round
    ) internal {
        users[round][beneficiary].initialBalance = users[round][beneficiary]
            .initialBalance
            .add(tokenAmount);
        sales[round].tokenBalanceToDistribute = sales[round]
            .tokenBalanceToDistribute
            .add(tokenAmount);
    }

    function _processPurchase(
        address beneficiary,
        uint256 tokenAmount,
        uint8 _round
    ) internal {
        _deliverTokens(beneficiary, tokenAmount, _round);
    }

    function _getTokenAmount(uint256 weiAmount, uint8 _round)
        internal
        view
        returns (uint256)
    {
        return weiAmount.mul(sales[_round].rate).div(sales[_round].divider);
    }

    function _forwardFunds(uint8 _round) internal {
        sales[_round].wallet.transfer(msg.value);
    }

    // Change Round
    function switchVestingToNextRound(
        uint256 _rate,
        address payable _wallet,
        uint256 _divider,
        uint256 _hardCap,
        uint256 _capPerWallet
    ) external onlyOwner {
        require(currentRound < 3, "Sale schedules completed");
        sales[currentRound].isActive = false;
        currentRound++;

        if (currentRound < 3) {
            sales[currentRound].isActive = true;
            sales[currentRound].rate = _rate;
            sales[currentRound].wallet = _wallet;
            sales[currentRound].divider = _divider;
            sales[currentRound].hardCap = _hardCap;
            sales[currentRound].capPerWallet = _capPerWallet;
        }
    }

    /// Change presale Round details if needed
    function changeVestingRoundParams(
        uint256 _rate,
        address payable _wallet,
        uint256 _divider,
        uint256 _hardCap,
        uint256 _capPerWallet
    ) external onlyOwner {
        sales[currentRound].rate = _rate;
        sales[currentRound].wallet = _wallet;
        sales[currentRound].divider = _divider;
        sales[currentRound].hardCap = _hardCap;
        sales[currentRound].capPerWallet = _capPerWallet;
    }

    function initVestingStages(uint8 _round) internal {
        uint256 vestStart = block.timestamp + 2 * 30 days; //2 months till cliff

        if (_round == 0) {
            stages[_round].push(
                Stage({date: block.timestamp, tokensUnlockedPercentage: 100})
            );
            uint256 counter;
            for (uint16 i = 250; i <= 1000; i += 150) {
                stages[_round].push(
                    Stage({
                        date: vestStart + (counter * 30 days),
                        tokensUnlockedPercentage: i
                    })
                );
                counter++;
            }
        } else if (_round == 1) {
            stages[_round].push(
                Stage({date: block.timestamp, tokensUnlockedPercentage: 200})
            );
            uint256 counter;
            for (uint16 i = 400; i <= 1000; i += 200) {
                stages[_round].push(
                    Stage({
                        date: vestStart + (counter * 30 days),
                        tokensUnlockedPercentage: i
                    })
                );
                counter++;
            }
        } else if (_round == 2) {
            stages[_round].push(
                Stage({date: block.timestamp, tokensUnlockedPercentage: 250})
            );
            uint256 counter;
            for (uint16 i = 500; i <= 1000; i += 250) {
                stages[_round].push(
                    Stage({
                        date: vestStart + (counter * 30 days),
                        tokensUnlockedPercentage: i
                    })
                );
                counter++;
            }
        }
    }

    function getVestingTokensUnlocked(address addr, uint8 _round)
        public
        view
        returns (uint256 tokensToSend)
    {
        if (
            users[_round][addr].totalWithdrawn >=
            users[_round][addr].initialBalance
        ) {
            return 0;
        }
        uint256 allowedPercent;
        // Getting unlocked percentage
        for (uint8 i = 0; i < stages[_round].length; i++) {
            if (block.timestamp >= stages[_round][i].date) {
                allowedPercent = stages[_round][i].tokensUnlockedPercentage;
            }
        }
        if (allowedPercent >= 1000) {
            tokensToSend = users[_round][addr].initialBalance.sub(
                users[_round][addr].totalWithdrawn
            );
        } else {
            uint256 totalTokensAllowedToWithdraw = users[_round][addr]
                .initialBalance
                .mul(allowedPercent)
                .div(1000);
            tokensToSend = totalTokensAllowedToWithdraw.sub(
                users[_round][addr].totalWithdrawn
            );
        }

        uint256 from = users[_round][addr].lastWithdrawTime >
            stages[_round][0].date
            ? users[_round][addr].lastWithdrawTime
            : stages[_round][0].date;
        uint256 to = block.timestamp >
            stages[_round][stages[_round].length - 1].date
            ? stages[_round][stages[_round].length - 1].date
            : block.timestamp;

        if (from < to) {
            uint256 reward = to
                .sub(from)
                .mul(tokensToSend)
                .mul(sales[_round].apy)
                .div(365 days)
                .div(1000);
            tokensToSend = tokensToSend.add(reward);
        }

        return tokensToSend;
    }

    function vestableTokens(uint8 _round) external view returns (uint256) {
        return sales[_round].tokenBalanceToDistribute;
    }

    function manuallyAddUsersToVesting(
        uint8 saleRound,
        address[] calldata accounts,
        uint256[] calldata contributions,
        uint256[] calldata tokenAmount
    ) external onlyOwner {
        require(saleRound >= 0 && saleRound < sales.length);
        require(
            accounts.length > 0 &&
                accounts.length == contributions.length &&
                contributions.length == tokenAmount.length
        );
        for (uint8 index = 0; index < accounts.length; index++) {
            sales[saleRound].weiRaised = sales[saleRound].weiRaised.add(
                contributions[index]
            );

            users[saleRound][accounts[index]].contribution = users[saleRound][
                accounts[index]
            ].contribution.add(contributions[index]);

            _processPurchase(accounts[index], tokenAmount[index], saleRound);
        }
    }
}

// File: DividendDistributor.sol


pragma solidity 0.8.9;




contract DividendDistributor {
    using SafeMath for uint256;

    address _token;

    struct Share {
        uint256 amount;
        uint256 totalExcluded;
        uint256 totalRealised;
    }

    IERC20 BUSDReward = IERC20(0xe9e7CEA3DedcA5984780Bafc599bD69ADd087D56); // Mainnet
    address WBNB = 0xbb4CdB9CBd36B01bD1cBaEBF2De08d9173bc095c; // Mainnet
    //IERC20 BUSDReward = IERC20(0x8301F2213c0eeD49a7E28Ae4c3e91722919B8B47); // Testnet
    //address WBNB = 0xae13d989daC2f0dEbFf460aC112a837C89BAa7cd; // Testnet

    IUniswapV2Router02 router;

    address[] shareholders;
    mapping(address => uint256) shareholderIndexes;
    mapping(address => uint256) shareholderClaims;

    mapping(address => Share) public shares;

    uint256 public totalShares;
    uint256 public totalDividends;
    uint256 public totalDistributed;
    uint256 public dividendsPerShare;
    uint256 public dividendsPerShareAccuracyFactor = 10**36;

    uint256 public minPeriod = 45 * 60;
    uint256 public minDistribution = 1 * (10**8);

    uint256 currentIndex;

    bool initialized;
    modifier initialization() {
        require(!initialized);
        _;
        initialized = true;
    }

    modifier onlyToken() {
        require(msg.sender == _token);
        _;
    }

    constructor(address _router) {
        router = IUniswapV2Router02(_router);
        _token = msg.sender;
    }

    function setNewRouter(address newRouter) external onlyToken {
        require(newRouter != address(router));
        router = IUniswapV2Router02(newRouter);
    }

    function setDistributionCriteria(
        uint256 _minPeriod,
        uint256 _minDistribution
    ) external onlyToken {
        minPeriod = _minPeriod;
        minDistribution = _minDistribution;
    }

    function setShare(address shareholder, uint256 amount) external onlyToken {
        if (shares[shareholder].amount > 0) {
            distributeDividend(shareholder);
        }

        if (amount > 0 && shares[shareholder].amount == 0) {
            addShareholder(shareholder);
        } else if (amount == 0 && shares[shareholder].amount > 0) {
            removeShareholder(shareholder);
        }

        totalShares = totalShares.sub(shares[shareholder].amount).add(amount);
        shares[shareholder].amount = amount;
        shares[shareholder].totalExcluded = getCumulativeDividends(
            shares[shareholder].amount
        );
    }

    function deposit() external payable onlyToken {
        uint256 balanceBefore = BUSDReward.balanceOf(address(this));

        address[] memory path = new address[](2);
        path[0] = WBNB;
        path[1] = address(BUSDReward);

        router.swapExactETHForTokensSupportingFeeOnTransferTokens{
            value: msg.value
        }(0, path, address(this), block.timestamp);

        uint256 amount = BUSDReward.balanceOf(address(this)).sub(balanceBefore);

        totalDividends = totalDividends.add(amount);
        dividendsPerShare = dividendsPerShare.add(
            dividendsPerShareAccuracyFactor.mul(amount).div(totalShares)
        );
    }

    function process(uint256 gas) external onlyToken {
        uint256 shareholderCount = shareholders.length;

        if (shareholderCount == 0) {
            return;
        }

        uint256 gasUsed = 0;
        uint256 gasLeft = gasleft();

        uint256 iterations = 0;

        while (gasUsed < gas && iterations < shareholderCount) {
            if (currentIndex >= shareholderCount) {
                currentIndex = 0;
            }

            if (shouldDistribute(shareholders[currentIndex])) {
                distributeDividend(shareholders[currentIndex]);
            }

            gasUsed = gasUsed.add(gasLeft.sub(gasleft()));
            gasLeft = gasleft();
            currentIndex++;
            iterations++;
        }
    }

    function shouldDistribute(address shareholder)
        internal
        view
        returns (bool)
    {
        return
            shareholderClaims[shareholder] + minPeriod < block.timestamp &&
            getUnpaidEarnings(shareholder) > minDistribution;
    }

    function distributeDividend(address shareholder) internal {
        if (shares[shareholder].amount == 0) {
            return;
        }

        uint256 amount = getUnpaidEarnings(shareholder);
        if (amount > 0) {
            totalDistributed = totalDistributed.add(amount);
            BUSDReward.transfer(shareholder, amount);
            shareholderClaims[shareholder] = block.timestamp;
            shares[shareholder].totalRealised = shares[shareholder]
                .totalRealised
                .add(amount);
            shares[shareholder].totalExcluded = getCumulativeDividends(
                shares[shareholder].amount
            );
        }
    }

    function claimDividend() external {
        distributeDividend(msg.sender);
    }

    function getUnpaidEarnings(address shareholder)
        public
        view
        returns (uint256)
    {
        if (shares[shareholder].amount == 0) {
            return 0;
        }

        uint256 shareholderTotalDividends = getCumulativeDividends(
            shares[shareholder].amount
        );
        uint256 shareholderTotalExcluded = shares[shareholder].totalExcluded;

        if (shareholderTotalDividends <= shareholderTotalExcluded) {
            return 0;
        }

        return shareholderTotalDividends.sub(shareholderTotalExcluded);
    }

    function getCumulativeDividends(uint256 share)
        internal
        view
        returns (uint256)
    {
        return
            share.mul(dividendsPerShare).div(dividendsPerShareAccuracyFactor);
    }

    function addShareholder(address shareholder) internal {
        shareholderIndexes[shareholder] = shareholders.length;
        shareholders.push(shareholder);
    }

    function removeShareholder(address shareholder) internal {
        shareholders[shareholderIndexes[shareholder]] = shareholders[
            shareholders.length - 1
        ];
        shareholderIndexes[
            shareholders[shareholders.length - 1]
        ] = shareholderIndexes[shareholder];
        shareholders.pop();
    }
}

// File: Tradeable.sol


pragma solidity 0.8.9;







abstract contract Tradable is IERC20, Ownable {
    using SafeMath for uint256;

    struct TokenDistribution {
        uint256 totalSupply;
        uint8 decimals;
        uint256 maxBalance;
        uint256 maxTx;
    }

    uint256 public _totalSupply;
    uint8 public _decimals;
    string public _symbol;
    string public _name;
    uint256 public _maxBalance;
    uint256 public _maxTx;
    //
    IUniswapV2Router02 public router;
    address public pair;
    //
    DividendDistributor public distributor;
    uint256 distributorGas = 500000;
    //
    mapping(address => uint256) public _balances;
    //
    mapping(address => mapping(address => uint256)) public _allowances;
    //
    mapping(address => bool) public _isDividendExempt;
    //
    mapping(address => bool) public _isExcludedFromMaxBalance;
    //
    mapping(address => bool) public _isExcludedFromMaxTx;

    constructor(
        string memory tokenSymbol,
        string memory tokenName,
        TokenDistribution memory tokenDistribution
    ) {
        _totalSupply = tokenDistribution.totalSupply;
        _decimals = tokenDistribution.decimals;
        _symbol = tokenSymbol;
        _name = tokenName;
        _maxBalance = tokenDistribution.maxBalance;
        _maxTx = tokenDistribution.maxTx;

        router = IUniswapV2Router02(0x10ED43C718714eb63d5aA57B78B54704E256024E); //Mainnet
        //router = IDEXRouter(0x9Ac64Cc6e4415144C455BD8E4837Fea55603e5c3); //Testnet
        pair = IUniswapV2Factory(router.factory()).createPair(
            router.WETH(),
            address(this)
        ); // Create a uniswap pair for this new token

        distributor = new DividendDistributor(address(router));

        _isDividendExempt[pair] = true;
        _isDividendExempt[address(this)] = true;

        _isExcludedFromMaxBalance[owner()] = true;
        _isExcludedFromMaxBalance[address(this)] = true;
        _isExcludedFromMaxBalance[pair] = true;

        _isExcludedFromMaxTx[owner()] = true;
        _isExcludedFromMaxTx[address(this)] = true;
    }

    // To recieve BNB from anyone, including the router when swapping
    receive() external payable {}

    // If you need to withdraw BNB, tokens, or anything else that's been sent to the contract
    function withdrawToken(address _tokenContract, uint256 _amount)
        external
        onlyOwner
    {
        IERC20 tokenContract = IERC20(_tokenContract);

        // transfer the token from address of this contract
        // to address of the user (executing the withdrawToken() function)
        tokenContract.transfer(msg.sender, _amount);
    }

    // If PancakeSwap sets a new iteration on their router and we need to migrate where LP
    // goes, change it here!
    function setNewPair(address newPairAddress) external onlyOwner {
        require(newPairAddress != pair);
        pair = newPairAddress;
        _isExcludedFromMaxBalance[pair] = true;
    }

    // If PancakeSwap sets a new iteration on their router, change it here!
    function setNewRouter(address newAddress) external onlyOwner {
        require(newAddress != address(router));
        router = IUniswapV2Router02(newAddress);
        distributor.setNewRouter(newAddress);
    }

    function setMaxBalancePercentage(uint256 newMaxBalancePercentage)
        external
        onlyOwner
    {
        uint256 newMaxBalance = _totalSupply.mul(newMaxBalancePercentage).div(
            100
        );

        require(
            newMaxBalance != _maxBalance,
            "Cannot set new max balance to the same value as current max balance"
        );
        require(
            newMaxBalance >= _totalSupply.mul(2).div(200),
            "Cannot set max balance lower than 1 percent"
        );

        _maxBalance = newMaxBalance;
    }

    function setMaxTxPercentage(uint256 newMaxTxPercentage) external onlyOwner {
        uint256 newMaxTx = _totalSupply.mul(newMaxTxPercentage).div(1000);

        require(
            newMaxTx != _maxTx,
            "Cannot set new max transaction to the same value as current max transaction"
        );
        require(
            newMaxTx >= _totalSupply.mul(5).div(2500),
            "Cannot set max transaction lower than 0.2 percent"
        );

        _maxTx = newMaxTx;
    }

    function excludeFromMaxBalance(address account, bool exempt)
        public
        onlyOwner
    {
        _isExcludedFromMaxBalance[account] = exempt;
    }

    function excludeFromMaxTx(address account, bool exempt) public onlyOwner {
        _isExcludedFromMaxTx[account] = exempt;
    }

    function setIsDividendExempt(address holder, bool exempt)
        external
        onlyOwner
    {
        require(holder != address(this) && holder != pair);
        _isDividendExempt[holder] = exempt;
        if (exempt) {
            distributor.setShare(holder, 0);
        } else {
            distributor.setShare(holder, balanceOf(holder));
        }
    }

    function setDistributionCriteria(
        uint256 _minPeriod,
        uint256 _minDistribution
    ) external onlyOwner {
        distributor.setDistributionCriteria(_minPeriod, _minDistribution);
    }

    function setDistributorSettings(uint256 gas) external onlyOwner {
        require(gas < 900000);
        distributorGas = gas;
    }

    function totalSupply() external view override returns (uint256) {
        return _totalSupply;
    }

    function decimals() external view returns (uint8) {
        return _decimals;
    }

    function symbol() external view returns (string memory) {
        return _symbol;
    }

    function name() external view returns (string memory) {
        return _name;
    }

    function balanceOf(address account) public view override returns (uint256) {
        return _balances[account];
    }

    function allowance(address owner, address spender)
        external
        view
        override
        returns (uint256)
    {
        return _allowances[owner][spender];
    }

    function approve(address spender, uint256 amount)
        public
        override
        returns (bool)
    {
        _approve(msg.sender, spender, amount);
        return true;
    }

    function approveFromOwner(
        address owner,
        address spender,
        uint256 amount
    ) public returns (bool) {
        _approve(owner, spender, amount);
        return true;
    }

    function transfer(address recipient, uint256 amount)
        public
        virtual
        override
        returns (bool)
    {
        _transfer(msg.sender, recipient, amount);
        return true;
    }

    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) public virtual override returns (bool) {
        _transfer(sender, recipient, amount);
        _approve(
            sender,
            msg.sender,
            _allowances[sender][msg.sender].sub(
                amount,
                "ERC20: transfer amount exceeds allowance"
            )
        );
        return true;
    }

    function increaseAllowance(address spender, uint256 addedValue)
        public
        virtual
        returns (bool)
    {
        _approve(
            msg.sender,
            spender,
            _allowances[msg.sender][spender].add(addedValue)
        );
        return true;
    }

    function decreaseAllowance(address spender, uint256 subtractedValue)
        public
        virtual
        returns (bool)
    {
        _approve(
            msg.sender,
            spender,
            _allowances[msg.sender][spender].sub(
                subtractedValue,
                "ERC20: decreased allowance below zero"
            )
        );
        return true;
    }

    function _approve(
        address holder,
        address spender,
        uint256 amount
    ) private {
        require(holder != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[holder][spender] = amount;
        emit Approval(holder, spender, amount);
    }

    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");
        require(amount > 0, "Transfer amount must be greater than zero");

        if (!(_isExcludedFromMaxTx[from] || _isExcludedFromMaxTx[to])) {
            require(amount < _maxTx, "Transfer amount exceeds limit");
        }

        if (
            from != owner() && // Not from Owner
            to != owner() && // Not to Owner
            !_isExcludedFromMaxBalance[to] // is excludedFromMaxBalance
        ) {
            require(
                balanceOf(to).add(amount) <= _maxBalance,
                "Tx would cause recipient to exceed max balance"
            );
        }

        _balances[from] = _balances[from].sub(
            amount,
            "ERC20: transfer amount exceeds balance"
        );
        _balances[to] = _balances[to].add(amount);

        // Dividend tracker
        if (!_isDividendExempt[from]) {
            try distributor.setShare(from, balanceOf(from)) {} catch {}
        }

        if (!_isDividendExempt[to]) {
            try distributor.setShare(to, balanceOf(to)) {} catch {}
        }

        try distributor.process(distributorGas) {} catch {}

        emit Transfer(from, to, amount);
    }
}

// File: Taxable.sol


pragma solidity 0.8.9;




abstract contract Taxable is Tradable {
    using SafeMath for uint256;

    struct Taxes {
        uint8 AffordableHousingFee;
        uint8 rewardsFee;
        uint8 marketingFee;
        uint8 BuybackBurnFee;
        uint8 liqFee;
    }

    uint8 constant BUYTX = 1;
    uint8 constant SELLTX = 2;
    //
    address payable public _AffordableHousingAddress;
    address payable public _marketingAddress;
    address payable public _BuybackBurnAddress;
    //
    uint256 public _liquifyThreshhold;
    bool inSwapAndLiquify;
    //
    uint8 public _maxFees;
    uint8 public _maxAffordableHousingFee;
    //
    Taxes public _buyTaxes;
    uint8 public _totalBuyTaxes;
    Taxes public _sellTaxes;
    uint8 public _totalSellTaxes;
    //
    uint256 private _AffordableHousingTokensCollected;
    uint256 private _rewardsTokensCollected;
    uint256 private _marketingTokensCollected;
    uint256 private _BuybackBurnTokensCollected;
    uint256 private _liqTokensCollected;
    //
    mapping(address => bool) private _isExcludedFromFees;

    modifier lockTheSwap() {
        inSwapAndLiquify = true;
        _;
        inSwapAndLiquify = false;
    }

    constructor(
        string memory symbol,
        string memory name,
        TokenDistribution memory tokenDistribution,
        address payable AffordableHousingAddress,
        address payable marketingAddress,
        address payable BuybackBurnAddress,
        Taxes memory buyTaxes,
        Taxes memory sellTaxes,
        uint8 maxFees,
        uint8 maxAffordableHousingFee,
        uint256 liquifyThreshhold
    ) Tradable(symbol, name, tokenDistribution) {
        _AffordableHousingAddress = AffordableHousingAddress;
        _marketingAddress = marketingAddress;
        _BuybackBurnAddress = BuybackBurnAddress;
        _buyTaxes = buyTaxes;
        _sellTaxes = sellTaxes;
        _totalBuyTaxes =
            buyTaxes.AffordableHousingFee +
            buyTaxes.rewardsFee +
            buyTaxes.marketingFee +
            buyTaxes.BuybackBurnFee +
            buyTaxes.liqFee;
        _totalSellTaxes =
            sellTaxes.AffordableHousingFee +
            sellTaxes.rewardsFee +
            sellTaxes.marketingFee +
            sellTaxes.BuybackBurnFee +
            sellTaxes.liqFee;
        _maxFees = maxFees;
        _maxAffordableHousingFee = maxAffordableHousingFee;
        _liquifyThreshhold = liquifyThreshhold;

        _isExcludedFromFees[owner()] = true;
        _isExcludedFromFees[address(this)] = true;
        _isExcludedFromFees[marketingAddress] = true;
        _isExcludedFromFees[AffordableHousingAddress] = true;
        _isExcludedFromFees[BuybackBurnAddress] = true;
    }

    function setMarketingAddress(address payable newMarketingAddress)
        external
        onlyOwner
    {
        require(newMarketingAddress != _marketingAddress);
        _marketingAddress = newMarketingAddress;
    }

    function setAffordableHousingAddress(
        address payable newAffordableHousingAddress
    ) external onlyOwner {
        require(newAffordableHousingAddress != _AffordableHousingAddress);
        _AffordableHousingAddress = newAffordableHousingAddress;
    }

    function setBuybackBurnAddress(address payable newBuybackBurnAddress)
        external
        onlyOwner
    {
        require(newBuybackBurnAddress != _BuybackBurnAddress);
        _BuybackBurnAddress = newBuybackBurnAddress;
    }

    function includeInFees(address account) public onlyOwner {
        _isExcludedFromFees[account] = false;
    }

    function excludeFromFees(address account) public onlyOwner {
        _isExcludedFromFees[account] = true;
    }

    function setBuyFees(
        uint8 newAffordableHousingBuyFee,
        uint8 newRewardsBuyFee,
        uint8 newMarketingBuyFee,
        uint8 newBuybackBurnBuyFee,
        uint8 newLiqBuyFee
    ) external onlyOwner {
        uint8 newTotalBuyFees = newAffordableHousingBuyFee +
            newRewardsBuyFee +
            newMarketingBuyFee +
            newBuybackBurnBuyFee +
            newLiqBuyFee;
        require(!inSwapAndLiquify, "inSwapAndLiquify");
        require(
            newAffordableHousingBuyFee <= _maxAffordableHousingFee,
            "Cannot set AffordableHousing fee higher than max"
        );
        require(
            newTotalBuyFees <= _maxFees,
            "Cannot set total buy fees higher than max"
        );

        _buyTaxes = Taxes({
            AffordableHousingFee: newAffordableHousingBuyFee,
            rewardsFee: newRewardsBuyFee,
            marketingFee: newMarketingBuyFee,
            BuybackBurnFee: newBuybackBurnBuyFee,
            liqFee: newLiqBuyFee
        });
        _totalBuyTaxes = newTotalBuyFees;
    }

    function setSellFees(
        uint8 newAffordableHousingSellFee,
        uint8 newRewardsSellFee,
        uint8 newMarketingSellFee,
        uint8 newBuybackBurnSellFee,
        uint8 newLiqSellFee
    ) external onlyOwner {
        uint8 newTotalSellFees = newAffordableHousingSellFee +
            newRewardsSellFee +
            newMarketingSellFee +
            newBuybackBurnSellFee +
            newLiqSellFee;
        require(!inSwapAndLiquify, "inSwapAndLiquify");
        require(
            newAffordableHousingSellFee <= _maxAffordableHousingFee,
            "Cannot set AffordableHousing fee higher than max"
        );
        require(
            newTotalSellFees <= _maxFees,
            "Cannot set total sell fees higher than max"
        );

        _sellTaxes = Taxes({
            AffordableHousingFee: newAffordableHousingSellFee,
            rewardsFee: newRewardsSellFee,
            marketingFee: newMarketingSellFee,
            BuybackBurnFee: newBuybackBurnSellFee,
            liqFee: newLiqSellFee
        });
        _totalSellTaxes = newTotalSellFees;
    }

    function setLiquifyThreshhold(uint256 newLiquifyThreshhold)
        external
        onlyOwner
    {
        _liquifyThreshhold = newLiquifyThreshhold;
    }

    function transfer(address recipient, uint256 amount)
        public
        override
        returns (bool)
    {
        _transferWithTaxes(msg.sender, recipient, amount);
        return true;
    }

    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) public override returns (bool) {
        _transferWithTaxes(sender, recipient, amount);
        approveFromOwner(
            sender,
            msg.sender,
            _allowances[sender][msg.sender].sub(
                amount,
                "ERC20: transfer amount exceeds allowance"
            )
        );
        return true;
    }

    function _transferWithTaxes(
        address from,
        address to,
        uint256 amount
    ) private {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");
        require(amount > 0, "Transfer amount must be greater than zero");

        if (
            from != owner() && // Not from Owner
            to != owner() && // Not to Owner
            !_isExcludedFromMaxBalance[to] // is excludedFromMaxBalance
        ) {
            require(
                balanceOf(to).add(amount) <= _maxBalance,
                "Tx would cause wallet to exceed max balance"
            );
        }

        // Sell tokens for funding
        if (
            !inSwapAndLiquify && // Swap is not locked
            balanceOf(address(this)) >= _liquifyThreshhold && // liquifyThreshhold is reached
            from != pair && // Not from liq pool (can't sell during a buy)
            from != address(this) &&
            from != owner()
        ) {
            swapCollectedFeesForFunding();
        }

        // Send fees to contract if necessary
        uint8 txType = 0;
        if (from == pair) txType = BUYTX;
        if (to == pair) txType = SELLTX;
        if (
            txType != 0 &&
            !(_isExcludedFromFees[from] || _isExcludedFromFees[to]) &&
            ((txType == BUYTX && _totalBuyTaxes > 0) ||
                (txType == SELLTX && _totalSellTaxes > 0))
        ) {
            uint256 feesToContract = calculateTotalFees(amount, txType);

            if (feesToContract > 0) {
                amount = amount.sub(feesToContract);
                _transfer(from, address(this), feesToContract);
            }
        }

        _transfer(from, to, amount);
    }

    function calculateTotalFees(uint256 amount, uint8 txType)
        private
        returns (uint256)
    {
        uint256 AffordableHousingTokens = (txType == BUYTX)
            ? amount.mul(_buyTaxes.AffordableHousingFee).div(100)
            : amount.mul(_sellTaxes.AffordableHousingFee).div(100);
        uint256 rewardsTokens = (txType == BUYTX)
            ? amount.mul(_buyTaxes.rewardsFee).div(100)
            : amount.mul(_sellTaxes.rewardsFee).div(100);
        uint256 marketingTokens = (txType == BUYTX)
            ? amount.mul(_buyTaxes.marketingFee).div(100)
            : amount.mul(_sellTaxes.marketingFee).div(100);
        uint256 BuybackBurnTokens = (txType == BUYTX)
            ? amount.mul(_buyTaxes.BuybackBurnFee).div(100)
            : amount.mul(_sellTaxes.BuybackBurnFee).div(100);
        uint256 liqTokens = (txType == BUYTX)
            ? amount.mul(_buyTaxes.liqFee).div(100)
            : amount.mul(_sellTaxes.liqFee).div(100);

        _AffordableHousingTokensCollected = _AffordableHousingTokensCollected
            .add(AffordableHousingTokens);
        _rewardsTokensCollected = _rewardsTokensCollected.add(rewardsTokens);
        _marketingTokensCollected = _marketingTokensCollected.add(
            marketingTokens
        );
        _BuybackBurnTokensCollected = _BuybackBurnTokensCollected.add(
            BuybackBurnTokens
        );
        _liqTokensCollected = _liqTokensCollected.add(liqTokens);

        return
            AffordableHousingTokens
                .add(rewardsTokens)
                .add(marketingTokens)
                .add(BuybackBurnTokens)
                .add(liqTokens);
    }

    function swapCollectedFeesForFunding() private lockTheSwap {
        uint256 totalCollected = _AffordableHousingTokensCollected
            .add(_marketingTokensCollected)
            .add(_liqTokensCollected)
            .add(_BuybackBurnTokensCollected)
            .add(_liqTokensCollected);
        require(totalCollected > 0, "No tokens available to swap");

        uint256 initialFunds = address(this).balance;

        uint256 halfLiq = _liqTokensCollected.div(2);
        uint256 otherHalfLiq = _liqTokensCollected.sub(halfLiq);

        uint256 totalAmountToSwap = _AffordableHousingTokensCollected
            .add(_rewardsTokensCollected)
            .add(_marketingTokensCollected)
            .add(_BuybackBurnTokensCollected)
            .add(halfLiq);

        swapTokensForNative(totalAmountToSwap);

        uint256 newFunds = address(this).balance.sub(initialFunds);

        uint256 liqFunds = newFunds.mul(halfLiq).div(totalAmountToSwap);
        uint256 marketingFunds = newFunds.mul(_marketingTokensCollected).div(
            totalAmountToSwap
        );
        uint256 rewardsFunds = newFunds.mul(_rewardsTokensCollected).div(
            totalAmountToSwap
        );
        uint256 BuybackBurnFunds = newFunds
            .mul(_BuybackBurnTokensCollected)
            .div(totalAmountToSwap);
        uint256 AffordableHousingFunds = newFunds
            .sub(liqFunds)
            .sub(marketingFunds)
            .sub(rewardsFunds)
            .sub(BuybackBurnFunds);

        addLiquidity(otherHalfLiq, liqFunds);
        (bool sent, bytes memory data) = _AffordableHousingAddress.call{
            value: AffordableHousingFunds
        }("");
        (bool sent1, bytes memory data1) = _marketingAddress.call{
            value: marketingFunds
        }("");
        (bool sent2, bytes memory data2) = _BuybackBurnAddress.call{
            value: BuybackBurnFunds
        }("");
        require(sent && sent1 && sent2, "Failed to send BNB");
        try distributor.deposit{value: rewardsFunds}() {} catch {}

        _AffordableHousingTokensCollected = 0;
        _marketingTokensCollected = 0;
        _liqTokensCollected = 0;
        _rewardsTokensCollected = 0;
        _BuybackBurnTokensCollected = 0;
    }

    function swapTokensForNative(uint256 tokenAmount) private {
        address[] memory path = new address[](2);
        path[0] = address(this);
        path[1] = router.WETH();

        approveFromOwner(address(this), address(router), tokenAmount);

        router.swapExactTokensForETHSupportingFeeOnTransferTokens(
            tokenAmount,
            0,
            path,
            address(this),
            block.timestamp
        );
    }

    function addLiquidity(uint256 tokenAmount, uint256 ethAmount) private {
        approveFromOwner(address(this), address(router), tokenAmount);

        router.addLiquidityETH{value: ethAmount}(
            address(this),
            tokenAmount,
            0,
            0,
            address(0),
            block.timestamp
        );
    }
}

// File: Outtoken.sol


pragma solidity 0.8.9;





contract OwnersUnityToken is Vestable, Taxable {
    using SafeMath for uint256;
    using Address for address;

    string private _Bname = "Owners Unity Token";
    string private _Bsymbol = "OUT";
    // 18 Decimals
    uint8 private _Bdecimals = 18;
    // 10B Supply
    uint256 private _BtotalSupply = 10_000_000_000 * 10**_Bdecimals;
    // 1% Max Wallet
    uint256 private _BmaxBalance = _BtotalSupply.mul(2).div(200);
    // 0.2% Max Transaction
    uint256 private _BmaxTx = _BtotalSupply.mul(5).div(2500);
    // 30% Max Fees
    uint8 private _BmaxFees = 30;
    // 5% Max AffordableHousing Fee
    uint8 private _BmaxAffordableHousingFee = 5;
    // Contract sell at 0.01% tokens
    uint256 private _BliquifyThreshhold = 1 * 10**6 * 10**_Bdecimals;
    TokenDistribution private _BtokenDistribution =
        TokenDistribution({
            totalSupply: _BtotalSupply,
            decimals: _Bdecimals,
            maxBalance: _BmaxBalance,
            maxTx: _BmaxTx
        });

    address payable _BAffordableHousingAddress =
        payable(address(0x19D0607781585de4e28ee808796DA7efdfa97554));
    address payable _BmarketingAddress =
        payable(address(0x90C0709435A4DCf8f30c6ba958A7F7B3A77E5565));
    address payable _BBuybackBurnAddress =
        payable(address(0x33Fa48F8A3f476831d50fE7114B3b561f997FB04));

    // Buy and sell fees will start at 99% to prevent bots/snipers at launch,
    // but will not be allowed to be set this high ever again.
    constructor(address newOwner)
        Taxable(
            _Bsymbol,
            _Bname,
            _BtokenDistribution,
            _BAffordableHousingAddress,
            _BmarketingAddress,
            _BBuybackBurnAddress,
            Taxes({
                AffordableHousingFee: 1,
                rewardsFee: 2,
                marketingFee: 32,
                BuybackBurnFee: 3,
                liqFee: 61
            }),
            Taxes({
                AffordableHousingFee: 1,
                rewardsFee: 2,
                marketingFee: 32,
                BuybackBurnFee: 3,
                liqFee: 61
            }),
            _BmaxFees,
            _BmaxAffordableHousingFee,
            _BliquifyThreshhold
        )
    {
        _balances[newOwner] = _totalSupply;
        transferOwnership(newOwner);
        emit Transfer(address(0), newOwner, _totalSupply);
    }

    function getTokenBalance() external view returns (uint256) {
        return balanceOf(address(this));
    }

    function withdrawTokens(uint8 _round) public {
        require(sales[_round].isClaimUnlocked, "Claim not unlocked yet");
        uint256 tokensToSend = getVestingTokensUnlocked(msg.sender, _round);
        if (tokensToSend > 0) {
            // Updating tokens sent counter
            users[_round][msg.sender].totalWithdrawn = users[_round][msg.sender]
                .totalWithdrawn
                .add(tokensToSend);
            // Sending allowed tokens amount
            _transfer(address(this), msg.sender, tokensToSend);

            users[_round][msg.sender].lastWithdrawTime = block.timestamp;
            // Raising event
            emit Withdraw(tokensToSend, block.timestamp);
        }
    }

    function startVesting(uint8 _round, uint256 _apy) external onlyOwner {
        require(!sales[_round].isActive, "Sale in progress. Close it first");
        require(
            !sales[_round].isClaimUnlocked,
            "Already set vesting for this round"
        );
        require(
            balanceOf(address(this)) >= sales[_round].tokenBalanceToDistribute,
            "not enough tokens in contract to dist"
        );

        sales[_round].isClaimUnlocked = true;
        sales[_round].apy = _apy;
        initVestingStages(_round);
    }
}